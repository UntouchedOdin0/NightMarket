plugins {
    id 'java'
    id 'idea'
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

group = 'me.byteful.plugin.nightmarket'
version = '1.0.0'

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name = 'jitpack'
        url = 'https://jitpack.io'
    }
    maven {
        name = 'redempt'
        url = 'https://redempt.dev'
    }
    maven {
        name = 'placeholder-api'
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
}

dependencies {
    // Spigot libs
    compileOnly 'org.spigotmc:spigot-api:1.13.2-R0.1-SNAPSHOT'

    // Helper libs
    implementation 'com.github.cryptomorin:XSeries:9.1.0'
    implementation 'com.github.Redempt:RedLib:6.5.8'

    // External plugin libs
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7'
    compileOnly 'me.clip:placeholderapi:2.11.2'

    // Datastore libs (compileOnly as they are injected/loaded during runtime to minimize JAR space and isolate from other server/plugin depends)
    compileOnly 'org.mongodb:mongo-java-driver:3.12.11'
    compileOnly 'mysql:mysql-connector-java:8.0.30'
    compileOnly 'org.xerial:sqlite-jdbc:3.39.3.0'
}

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('*.yml') {
        expand props
    }
}

shadowJar {
    minimize()
    archiveBaseName.set("NightMarket")
    archiveClassifier.set("")

    relocate 'com.cryptomorin.xseries', 'me.byteful.plugin.nightmarket.libs.xseries'
    relocate 'redempt', 'me.byteful.plugin.nightmarket.libs.redempt'
}